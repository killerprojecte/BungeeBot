plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

group = 'org.rgbmc'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven{
        url = "https://repo.fastmcmirror.org/content/repositories/releases/"
    }
    maven {
        url 'https://repo.clojars.org'
        name 'Clojars'
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.j256.ormlite:ormlite-core:6.1'
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    implementation 'hikari-cp:hikari-cp:2.14.0'
    implementation 'com.mysql:mysql-connector-j:8.0.31'
    implementation 'org.xerial:sqlite-jdbc:3.40.0.0'
    compileOnly 'net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT'
    implementation 'com.xbaimiao:mirai-http-sdk:1.0.4-beta-2bb9815'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('bungee.yml') {
        expand props
    }
}

shadowJar {
    relocate("com.google.gson", "org.rgbmc.bungeebot.gson")
    relocate("com.j256.ormlite", "org.rgbmc.bungeebot.ormlite")
    relocate("com.zaxxer.hikari", "org.rgbmc.bungeebot.hikari")
    relocate("com.xbaimiao.mirai", "org.rgbmc.bungeebot.mirai")
    archiveClassifier = ""
}

